#!/usr/bin/perl
# PODNAME: vote-count
# command line interface to run PrefVote::STV processing YAML input for testing and simple usage example
# derived from Vote::STV by Ian Kluft
# Copyright (c) 1998-2021 by Ian Kluft
# Open Source license: Apache License 2.0 https://www.apache.org/licenses/LICENSE-2.0

use Modern::Perl qw(2015); # require 5.20.0 or later
use autodie;
use Carp qw(croak confess);;
use YAML::XS; # RPM: perl-YAML-LibYAML, DEB: libyaml-libyaml-perl
use PrefVote::Core;
use Getopt::Long;
use Data::Dumper;

# print results list (winners or eliminated) with potential ties
sub print_result_list
{
    my $result_list = shift;
    if (scalar @$result_list) {
        foreach (my $i=0; $i < scalar @$result_list; $i++) {
            my $place = $i+1;
            if (scalar @{$result_list->[$i]} > 1) {
                say "$place  tie: ".join(" ", sort @{$result_list->[$i]});
            } else {
                say "$place: ".$result_list->[$i][0];
            }
        }
    } else {
        say "none";
    }
    return;
}

# command-line processing
my ($debug, $test_mode);
GetOptions("debug" => \$debug, "test" => \$test_mode);
if (not @ARGV) {
    say STDERR "usage: $0 [--debug] [--test] yaml_file";
    exit 0;
}

# count votes
my $vote_obj = PrefVote::Core::yaml2vote($ARGV[0]);
if ($debug) {
    # set debugging in PrefVote hierarchy
    $vote_obj->debug(1);

    # turn on stack traces for unexpected errors
    ## no critic (Variables::RequireLocalizedPunctuationVars)
    $SIG{ __DIE__ } = sub { Carp::confess( @_ ); };
}
$vote_obj->count();
if ($vote_obj->debug()) {
    say STDERR "returned vote object is a ".ref($vote_obj);
    say STDERR Dumper($vote_obj);
}

# print result
my $results = $vote_obj->results();
say "Winning order";
print_result_list($results->{winners});
say "Elimination order";
print_result_list($results->{eliminated});

__END__

# POD documentation

=head1 NAME

vote-count

=head1 USAGE

vote-count vote-data.yaml

=head1 OPTIONS

=head1 EXIT STATUS

=head1 SEE ALSO

=head1 BUGS AND LIMITATIONS

Please report bugs via GitHub at L<https://github.com/ikluft/prefvote/issues>

Patches and enhancements may be submitted via a pull request at L<https://github.com/ikluft/prefvote/pulls>

=cut
